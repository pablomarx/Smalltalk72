	.TITL UPDATETIMER
;
; O.S. code for updating timers
; P. Deutsch  31-DEC-74
;
	.ENT UPDATETIMER
;
	.SREL
UPDATETIMER:UDT
;
	.NREL
;
; Move excess time from hardware clock to calendar.
; Must be called at least once per second,
; preferably from field interrupt.
UDT:	RCLK
	LDA 1,@RTB0
	SUB 1,0
	LDA 1,SECHI
	ADCZ# 1,0,SNC
	JMP 1,3	; Hasn't reached 1 second yet
	LDA 0,@RTB0
	ADD 1,0
	STA 0,@RTB0	; Update base
	LDA 1,@RTB1
	LDA 0,SECLO
	ADDZ 0,1,SZC	; 32-bit add
	ISZ @RTB0
	JMP .+1
	STA 1,@RTB1
	LDA 1,@CMIL1
	LDA 0,THOU
	ADDZ 0,1,SZC	; 32+16 bit add
	ISZ @CMIL0
	JMP .+1
	STA 1,@CMIL1	; Update ms calendar
	ISZ @CSEC1	; Update seconds calendar
	JMP 1,3
	ISZ @CSEC0
	JMP 1,3
	JMP 1,3
;
; Pointers to reserved page 1 locations
CSEC0:	572	; Seconds calendar
CSEC1:	573
CMIL0:	574	; Milliseconds calendar
CMIL1:	575
RTB0:	576	; Base for RTC
RTB1:	577
;
; Funny constants
SECHI:	25.	; 1 second in units of .595 microseconds
SECLO:	42272.
THOU:	1000.
RTMSK:	-100	; Mask for lower half of hardware clock
;
	.END
