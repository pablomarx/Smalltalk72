;// FILE MAINTENANCE --   SETXY.SR
;//THIS ROUTINE MAKES THE DESTINATION WORD ADDRESS AND DEST.
;//BIT ADDRESS IN THE SCAN CONVERTION CONTROL TABLE CORRECT.
;//SETXY MAY BE CALLED EITHER BY SMALLTALK OR BY ASM LANGUAGE
;//	to setxy x y (:x.:y. CODE ??)

;//FOR A CALL FROM ASM LANG IT EXPECT TO GET PARAMETERS FROM A 
;//THREE ENTRY |TABLE POINTED TO BE AC3 --
;//	TABLE:
;//	X:	0
;//	WIDTH:	0
;//	Y:	0
;//THE WIDTH PARAMETER MAY BE A DUMMY AS IT IS IGNORED IN
;//EVERY RESPECT SAVE THAT X & Y NEED TO BE SEPARATED BY ONE AD
;//THE CALL MIGHT LOOK LIKE --
;//	JSRII .STXY
;//PRT:	TABLE
;//	JMP CONT . . .

.TITL SETXY
.GETNOLIST "SMALL.OPS"
.GETNOLIST "SMALL.SYMS"
.GETNOLIST "SMDISP.SYMS"

.SREL
SETXY:	SETXYC
STSXY:	STSXYC
SETX:	0
SETW:	0
SETY:	0
SETH:	0
SETV:	0

.NREL
 

RETRN:	0		;//PLACE FOR RETURN FROM ASM CODE CALL

FIFTN:	17		;//UTILITY VALUES
SIXTN:	20

.CNTB:	CTB0		;//EXTERNAL ADDRESSES
.SETW:	SETW
.SETY:	SETY
.INDEX:	INDEX
.MDDM:	MDDIM
.GTMS:	GTMES
.DSPWD:	DWDTH

STSXYC:
	JSRII .GTMS	;//PULL IN ARGUMENTS
	2
	SETX

	LDA 0,@.SETW	;//GET Y INTO RIGHT PLACE
	STA 0,@.SETY
	JSR SETXYC
	SETX
	JMP @.EVAL
 
SETXYC:
	STA 3,RETRN	;//SAVE RETURN FOR ASM CODE CALL
	LDA 3,0,3	;//GET PTR TO X,W,Y,H TABLE
	STA 3,XYTBL
	JSRII .INDEX	;//COMPUTE FIRST DWA
XYTBL:	0		;//RESULT IN AC0
	LDA 3,.CNTB	;//GET CONTROL TABLE AD
	LDA 2,@.DSPWD	;//GET DISP WIDTH
	SUBZ 2,0	;//SUB FROM DEST WORD AD
	STA 0,2,3	;//SAVE DWA MINUS DISP WDTH IN CTRL TBL
 
	LDA 2,@RETRN	;//GET TABLE PTR BACK
	LDA 1,0,2	;//GET TO CORRECT BIT ADDRESS
	MOVZ 1,0	;//DBA = 15-(SETX-(SETX(MOD 16))
	JSRII .MDDM
	SUBZ 0,1
	LDA 2,FIFTN	;//DEST BIT AD MUST BE 15-0
	SUBZ 1,2
	LDA 3,.CNTB	;//GET CONTROL TABLE AD
	STA 2,1,3	;//SAVE DEST BIT AD
 
	LDA 3,RETRN	;//AND BACK
	JMP 1,3
 
 .END
X