
;//FILE MAINTENANCE --  SCROL.SR
;//ROUTINE FOR CAUSING SCROLLING WITHIN A FRAME
;//ACTUALLY DOES A MEASURE OF THE FIRST LINE IN THE STRING
;//DOES A MOVE AND CLEAR UP ONE LINE--RESCANS THE LAST LINE
;//AT THE UPDATED Y AND AND FIXES UP LAST AND LAST LINE
;//AND OTHER MISC. GOODIES

.TITL SCROL

.GETNOLIST "SMALL.OPS"
.GETNOLIST "SMALL.SYMS"
.GETNOLIST "SMDISP.SYMS"

.SREL
SCROL:	SCROLC
STSCL:	STSCLC
SCSTG:	0
SLBD:	1
SRBD:	0
TBLSS:		;//TABLE FOR SSCAN WHEN UPDATING BUF
	1	;//MEANS GO FORWARD
	5	;//MEANS STRING 1 GETS STRING 2
	0	;//CONSTANT N.A. IN SCROL
DSBUF:	0	;//DEST STRING
	1	;//START AT BEGINNING
DCLST:	0	;//TO LAST MINUS TOPLEN
SRCBF:	0	;//SOURCE STRING
TPLEN:	0	;//START AT TOP LENGTH
CRLST:	0	;//GO TO LAST BEFORE TOP LENGTH SUBSTRACTED

SMVX:	0
SMVW:	0
SMVY:	0
SMVH:	0
DMVX:	0
DMVW:	0
DMVY:	0
DMVH:	0
MVMD:	0


.NREL

-1
.GTIN:	GTINS		;//EXTERNALS AND SUCH
.CKPR:	CKPAR
.PSTG:	PSTRG
.UPDT:	UPDAT
.FRMX:	FRMX
.FRWD:	FRWD
.LAST:	LAST
.LSLN:	LSLN
.MARK:	MARK
.CHRY:	CHRY
.BFLN:	BFLN
.MZUR:	MAZUR
.GTHT:	GETHT
.RELX:	REALX
.RLWD:	RELWD
.RELY:	REALY
.RLHT:	RELHT
.MODE:	MOOD
.BMVC:	BMVCL
.SCTB:	SCSTG
.BUF:	BUF
.SCSTG:	SCSTG
.SRBD:	SRBD

.DSBUF:	DSBUF
.DCLST:	DCLST
.SRCBF:	SRCBF
.TPLEN:	TPLEN
.CRLST:	CRLST

			;//TBL FOR BLOCK MOVE AND CLEAR OPERATION
.SMVX:	SMVX		;//REALX
.SMVW:	SMVW		;//RELWD
.SMVY:	SMVY		;//RELY+FONT HEIGHT
.SMVH:	SMVH		;//RELHT-FONT HEIGHT
.DMVX:	DMVX		;//RELX
.DMVW:	DMVW		;//RELWD
.DMVY:	DMVY		;//RELY
.DMVH:	DMVH		;//RELHT-FONT HEIGHT

CR:	15
BCSP:	10
RETRN:	0

STSCLC:
	JSRII .GTIN	;//IF CALLED FROM SMALLTALK GET SET UP
	JSRII .CKPR
	JSR SCROLC
	JSRII .PSTG	;//SCAN IN LAST LINE AT NEW Y
	JSRII .UPDT	;//BE SURE EVERYTHING UPDATED

SCROLC:
	STA 3,RETRN	;//SAVE RETURN AD
	LDA 0,@.BUF	;//SET UP BUF PTR FOR SSCAN AND MAZUR
	STA 0,@.DSBUF
	STA 0,@.SRCBF
	STA 0,@.SCSTG
	LDA 0,@.BFLN	;//SET UP RIGHT BOUNDARY OF BUF STRING
	STA 0,@.SRBD
	LDA 2,@.FRMX	;//SET UP RIGHT MARGIN FOR MAZUR
	LDA 0,@.FRWD	;//MAZUR WANTS RT MGN TO WRK WITH(X+FRMD)
	ADDZ 2,0	;//AND STARTING X IN AC2
	LDA 1,.SCTB	;//PTR TO TABLE IN AC1
	JSRII .MZUR	;//GET BYTE PTR TO BEGINNING OF SCND LINE
	LDA 3,CR	;//WANT CR'S TO SIT AT END OF LINES SO
 	SNE 0,3		;//INC MAZUR'S COUNT IF OVERFLOW ON CR
	INC 1,1
	STA 1,@.TPLEN	;//TOP LGTH IS LFT BYTE PTR FOR SRC SBSTG
	NEG 1,1		;//NEED TOP LENGTH - 1
	COM 1,1
	LDA 0,@.LAST	;//ALSO VALUE TO DEC LAST (MINUS 1, I.E.)
	STA 0,@.CRLST	;//BUT THS LST TO BE END PT FOR SRC SBSTG
	SUBZ 1,0	;//AND DEC'D VERSION FOR DEST STRING
	SP 0,0		;//SEE IF WE'VE GONE NEGATIVE
	ZER 0,0		;//WHICH MEANS UP AGAINST THE BEGINNING
	STA 0,@.DCLST	;//DEC LAST IS STOPPING PT FOR DEST SBSTG
	STA 0,@.LAST	;//AND UPDATE LAST
	LDA 0,@.MARK	;//UPDATE MARK
	SUBZ 1,0
	SP 0,0		;//KEEP POSITIVE
	ZER 0,0
	STA 0,@.MARK
	LDA 0,@.LSLN	;//UPDATE LAST LINE MARKER
	SUBZ 1,0
	SGZ 0,0		;//NOT TO GET LESS THAN ONE
	ONE 0,0
	STA 0,@.LSLN
	JSRII .GTHT	;//UPDATE CHARY--CUR CHARY - FONT HEIGHT
	LDA 1,@.CHRY
	SUBZ 0,1
	LDA 0,@.RELY	;//BE SURE CHARY NOT ABOVE TOP OF WINDOW
	SGT 1,0
	MOVZ 0,1
	STA 1,@.CHRY
 
	JSR @.SSCAN	;//FIX STRING
	TBLSS
 
	JSRII .UPDT	;//NON-NORMAL RETURN--SHOULDN'T HAPPEN
	LDA 0,@.RELX	;//SET UP TABLE FOR BLOCK MOVE AND CLEAR
	STA 0,@.SMVX
	STA 0,@.DMVX
	LDA 0,@.RLWD
	STA 0,@.SMVW
	STA 0,@.DMVW
	LDA 1,@.RELY
	STA 1,@.DMVY	;//DEST Y IS TOPY
	JSRII .GTHT
	ADDZ 0,1	;//SOURCE Y IS TOPY + FONT HEIGHT
	STA 1,@.SMVY
	LDA 1,@.RLHT	;//HEIGHT IN BOTH SRC AND DEST IS ACTUAL
	SUBZ 0,1	;//HEIGHT - FONTHEIGHT
	SP 1,1		;//BE SURE STAYS POSITIVE
	ZER 1,1
	STA 1,@.SMVH
	STA 1,@.DMVH
 
	JSRII .BMVC	;//DO BLOCK MOVE AND CLEAR
	SMVX
	DMVX
 
	NIL 0,0		;//GET READY FOR CLRNG REDO OF LAST LINE
	STA 0,@.MODE
 
	JMP @RETRN	;//AND GO
	
.ENX