
;//FILE MAINTENANCE --    INSEC.SR
;//RTNE FOR CHKNG FOR THE INTRSCTN OF 2 RECTANGLES ON THE BASIS
;//OF THEIR X',WIDTH'S,Y'S, AND HEIGHT'S IN THAT ORDER.
;//IF THERE IS INTRSCTN, 4 VALUES ARE RTRND DENOTING WHERE THE 
;//INTERSECTIONS OCURRED.
;//A CALL MIGHT LOOK SOMETHING LIKE THE FOLLOWING:

;//TBMAJ:
;//MAJX:	0
;//MAJWD:	0
;//MAJY:	0
;//MAJHT:	0

;//TBMIN:
;//MINX:	0
;//MNWD:	0
;//MINY:	0
;//MNHT:	0

;//INTER:
;//INTRX:	0
;//INTWD:	0
;//INTRY:	0
;//INTHT:	0

;//	JSRII .INSC
;//.TBMJ:	TBMAJ		PTR TO FIRST TABLE TO BE COMPARED
;//.TBMN:	TBMIN		PTR TO SCND TABLE TO BE COMPARED
;//.INTR:	INTER		PTR TO TBL WHERE INSCTN VALS PUT

;//	JMP NOINT	NOSKP RETURN MEANS NO INTERSECTION
;//	JMP DIDIT	MEANS THAT THERE WAS SOME INTERSECTION
			;//OF THE TWO RECTANGLES

;//THE COPY ENTRY HERE SIMPLY ALLOWS THE COPYING OF ONE FOUR
;//ENTRY TABLE ONTO A DESTINATION TABLE  --  INTERSECT NOSKP'S
;//WHEN SRC AND TBMAJ AND TBMIN ARE THE SAME.


.TITL INSEC
.GETNOLIST "SMALL.OPS"
.GETNOLIST "SMALL.SYMS"
.GETNOLIST "SMDISP.SYMS"
.SREL
INSEC:	INSECC
COPY:	COPYC

.NREL

.TBMJ:	0		;//PLACE FOR TABLE PTRS
.TBMN:	0
.INTR:	0

REPRO:	SP 1,1
INTER:	SGZ 1,1

RETRN:	0		;//PLACE FOR RETURN FROM INSEC
TEMP:	0		;//PLACE FOR RTRN FROM INTERNAL SUBROUTINE

COPYC:
	LDA 0,REPRO	;//MAKE SKP POS TEST FOR INTER
	STA 0,WHICHONE
	JMP BOTH

INSECC:
	LDA 0,INTER
	STA 0,WHICHONE	;//MAKE SGZ TEST FOR INTER
BOTH:
	LDA 0,0,3	;//GET IN TABLE PTRS
	STA 0,.TBMJ
	INC 3,3
	LDA 0,0,3
	STA 0,.TBMN
	INC 3,3
	LDA 0,0,3
	STA 0,.INTR
	INC 3,3
	STA 3,RETRN	;//SAVE RETURN

	JSR CMPAR	;//FRST CHCK IF X'S AND WDTH'S INTRSCT
	ISZ .TBMJ	;//INC TO Y PARAM IN TABLES
	ISZ .TBMN
	ISZ .INTR
	JSR CMPAR	;//NOW CHECK Y AND HEIGHT INTERSECTS
	LDA 3,RETRN	;//IF IT GETS BACK THERE IS INTERSECTION
	JMP 1,3		;//FOR WHICH THERE IS A SKIP RETURN


CMPAR:
	STA 3,TEMP	;//SAVE RETURN
	LDA 3,@.TBMJ	;//GET MAJOR X OR MAJOR Y
	ISZ .TBMJ	;//GET TO WIDTH OR HEIGHT PARAM
	LDA 2,@.TBMJ
	ADDZ 3,2	;//SQUIRREL MAJOR RT OR BT IN AC2

	LDA 0,@.TBMN	;//NOW GET MINOR X OR Y
	ISZ .TBMN	;//GET TO WIDTH OR HEIGHT
	LDA 1,@.TBMN	;//ADD TOGETHER AND
	ADDZ 0,1	;//SQUIRREL AWAY IN AC1

	SGE 0,3		;//FIND LARGER X OR Y
	MOVZ 3,0	;//THE LARGER IS THE ONE SAVED
	STA 0,@.INTR
	ISZ .INTR	;//INC TABLE PTR TO WIDTH PARAM

	SLE 1,2		;//SAVE LESS OF RIGHT MARGINS OR BOTTOMS
	MOVZ 2,1
	SUBZ 0,1	;//INTERSECTION  WIDTH OR HEIGHT IS 
			;//LSSR RT MRGN OR BTM - INSCTN X OR Y
WHICHONE:
	SP 1,1		;//IF NEG QUOTIENT NO INTRSCTN
			;//SP WHEN COPYING -- SGZ WHEN INTERSCNG
	JMP @RETRN	;//NO SKP RETURN
	STA 1,@.INTR	;//SAVE INTERSECTION WIDTH OR HEIGHT

	JMP @TEMP	;//SECOND TIME GETS THE Y'S AND HEIGHTS

.END
