
;//FILE MAINTENANCE --      FSCAN.SR
;//THIS IS A ROUTINE TO DO SCAN CNVRSN ON EITHER PROPORTIONAL OR
;//NON-PRPRTNL FONTS.  IT WILL WORK ONLY ON THE ALTO AND CALLS
;//THE MICROCODE CONVERT ROUTINE
;//JUSTIFY ENTRY TAKES A WIDTH AC3+1 AND BUMPS DBA AND DWA 
;//ACCORDINGLY


.TITL SCAN

.GETNOLIST "SMALL.OPS"
.GETNOLIST "SMALL.SYMS"
.GETNOLIST "SMDISP.SYMS"

.SREL
CTB0:	0
CTB1:	0
CTB2:	0
CHR:	0
SCAN:	SCANC
JUSTIFY:	JUSTIFYC




.NREL

-1
.CHR:	CHR	;//AD OF CHAR CODE
SCRTN:	0	;//PLACE TO SAVE RETURN
SIXTN:	20	;//UTILITY 16

.CNTB:	CTB0	;//AD OF CONTROL TABLE--USED BY MICROCODE

;//CONTB:
;//CTB0:	0
;//CTB1:	0
;//CTB2:	0
	;//FRST ENTRY = DSP WDTH AS IN ENTRY IN OPN ROUTINE
	;//SECOND ENTRY = DESTINATION BIT ADDRESS (DBA)--15-0
	;//THIRD ENTRY = DES WORD ADDRESS (DWA) MINUS DISP WIDTH
		;//MCROCD DOESN'T USE ENTRY--HERE FOR CONVENIENCE


SCANC:
	STA 3,SCRTN	;//SAVE RETURN
	LDA 0,@.CHR	;//GET CHAR IN AC0
	LDA 2,.CNTB	;//GET AD OF CONTB INTO AC2
	LDA 3,.FONT	;//GT FIRST AD OF FONT INTO AC3
	ADD 0,3		;//INDEX BY CHAR
	LDA  0,2,2	;//GET DWA MINUS DISPW INTO AC0
SCNAGAIN:
	CONVERT 0   	;//CALL CONVERSION ROUTINE
	JMP EXT		;//RETURN HERE IF CHAR HAS EXTENTION
JUSTIT:
	SUBZ 3,1 SZC	;//AC3 HAS WIDTH, AC1 DBA
			;//SKIPS FOR FXUP IF WRD BOUNDARY CROSSED
	JMP ENDCH	;//OTHERWISE GO TO FINISH
FIXUP:
	INC 0,0		;//INC DWA -- WORD BOUNDARY CROSSED
	LDA 3,SIXTN	;//ADD 16 TO DBA
	ADD 3,1
ENDCH:
	STA 0,2,2	;//SAVE DWA
	STA 1,1,2	;//SAVE DBA
	JMP @SCRTN	;//RETURN TO CALLER
EXT:
	LDA 1,.FONT	;//GET FIRST AD OF FONT FOR 
			;//INDXNG WITH EXT CODE
	ADD 1,3
	INC 0,0		;//INC DWA -- WRD BOUNDARY CROSSED IF EXT
	JMP SCNAGAIN	;//DBA OK SNCE EXT ONLY RECHED IF LST CHR
			;//16 BITS WIDE--AC2 UNCHANGED BY
			;//CONVERT SO
			;//NO NEED TO RFRSH--GO DO IT WITH EXT CHR

JUSTIFYC:
	STA 3,SCRTN	;//CODE FOR GETTING CONTROL TABLE RIGHT
			;//WHEN JUSTIFYING REQUESTED--AMT TO INC
			;//IN AC3+1
	ISZ SCRTN	;//FIXUP RETURN
	LDA 3,0,3	;//GET REQUISITE WIDTH IN AC3
	LDA 2,.CNTB	;//GET CONTROL TABLE ADDRESS
	LDA 0,2,2	;//GET DWA
	LDA 1,SIXTN	;//IF REQUIRED WIDTH > 16 INC WORD AND TRY
	SLE 3,1		;//AGAIN
	JMP WRDINC
GOJUST:
	LDA 1,1,2	;//OTHERWISE GO TO CHECK FOR WORD BOUND
	JMP JUSTIT	;//CROSSING -- DO FIXUPS THERE AND RETURN
			;//THROUGH SCRTN

WRDINC:
	INC 0,0		;//HERE'S WHERE WE WORK WIDTH DOWN TO LESS
	SUBZ 1,3	;//THAN 16
	SLE 3,1
	JMP WRDINC
	JMP GOJUST

.END
  	 	