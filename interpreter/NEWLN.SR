;//FILE MAINTENANCE --   NEWLN.ED     NEWLN.SR
;//ROUTINE FOR UPDATING CONVERT CNTRL TBL ON THE BASIS OF A NEW
;//Y CAUSED BY LINE OVERFLOW OR CAR RETRN
;//ALSO  FIXES FRM HEIGHT PARAM IF CURRENT HEIGHT IS OVERFLOWED
;//SKP RTRN IS NRML -- NOSKP MEANS THAT BTM OF WNDOW IS OVRFLWD

.TITL NEWLN
.GETNOLIST "SMALL.OPS"
.GETNOLIST "SMALL.SYMS"
.GETNOLIST "SMDISP.SYMS"

.SREL
NEWLN:	NEWLNC
.NREL
-1
.GTHT:	GETHT		;//EXTERNALS
.INY:	INY
.WNBT:	WNBT
.FRBT:	FRBT
.STXY:	SETXY
.FRMY:	FRMY
.FRHT:	FRHT
.CHRY:	CHRY
.WNHT:	WNHT
.HTSW:	HITSW		;//SWITCH FOR FRAME HEIGHT CHANGE
			;//INIT'D IN SETUP ROUTINE
RETRN:	0

NEWLNC:
	STA 3,RETRN	;//SAVE RETURN
	JSRII .GTHT	;//GET FONT HGHT--MIGHT ALSO BE CALLED TO
			;//FIND GREATEST HEIGHT IN A GIVEN LINE
	LDA 1,@.INY	;//GET Y OF CURRENT LINE
	ADDZ 0,1	;//CHECK FRST FOR OVERFLOW OF WINDOW BTM
	LDA 2,@.WNBT
	SUBZ 0,2	;//BTM OF WINDOW MINUS FONT HEIGHT GETS
			;//MEANINGFUL Y
	SLE 1,2
	JMP @RETRN	;//NOSKP MEANS WINDOW OVERFLOW
	STA 1,@.INY	;//UPDATE LINE Y
	STA 1,@.CHRY	;//RUNNING UPDAT OF CHRY--SO SCRLNG WORKS

	LDA 2,@.FRBT	;//NOW CHECK FOR OVERFLOW OF FRAME BOTTOM
	SUBZ 0,2	;//BOTTOM OF FRAME MINUS FONT HEIGHT GETS
			;//MEANINGFUL Y
	SLE 1,2
	JSR FXHIT

	LDA 3,RETRN	;//NORMAL RETURN IS SKPRTN
	JMP 1,3

FXHIT:
	SUBZ 2,1	;//ADD DIFFERENCE BETWEEN FRAME BOTTOM
	LDA 0,@.FRHT	;//AND OVERFLOW TO FRM HEIGHT AND BTM
	ADDZ 0,1	;//AC1 NOW = FRBT
	LDA 2,@.FRMY
	ADDZ 1,2	;//AC2 NEW FRHT
	LDA 0,@.WNBT	;//FRBT>WNBT?(FRBT_WNBT)
	SLE 2,0
	JMP FMOVR
	STA 1,@.FRHT	;//SAVE NEW FRHT
	STA 2,@.FRBT	;//AND FRBT
FXOUT:
	ONE 0,0
	STA 0,@.HTSW
	JMP 0,3
FMOVR:
	STA 0,@.FRBT	;//FRBT>WNBT?(FRBT_WNBT)
	SUBZ 0,2	;//FRHT_FRHT - (WNBT-OVERFLOW FRBT)
	SUBZ 2,1
	STA 1,@.FRHT
	JMP FXOUT
 
.END 