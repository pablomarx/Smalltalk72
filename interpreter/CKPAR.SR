
;//FILE MAINTENANCE --  CKPAR.SR
;//ROUTINE FOR CHECKING WINDOW AND FRAME PARAMETERS
;//INTERSECTS THE WINDOW AND THE DISPLAY -- THE LESSER OF ANY
;//COMPARED PARAMETERS ARE THOSE USED FOR SCAN CONVERSION.
;//FOR INSTANCE, IF WINDOW X IF LESS THAN ZERO, ZERO WILL BE 
;//CONSIDERED TO BE WINDOW X
;//IF COMBINATIONS OF X AND WIDTH AND/OR Y AND HEIGHT DO NOT
;//YIELD INTERSECTION--NO SCAN CONVERSION WILL TAKE PLACE

;//IF THE REPLY IN THE INSTANCE IS:
	;//0 -- REQUESTED ACTION HAPPENED
	;//1 -- WINDOW AND DISPLAY HAVE NO INTERSECTION
	;//2 -- "ADJUSTED" WINDOW AND FRAME HAVE NO INTERSECTION

.TITL CKPAR

.GETNOLIST	"SMALL.OPS"
.GETNOLIST	"SMALL.SYMS"
.GETNOLIST	"SMDISP.SYMS"

.SREL
CKPAR:	CKPARC
REALX:	0	;//PLACE FOR "ADJUSTED" WINDOW PARAMS
RELWD:	0
REALY:	0
RELHT:	0
CLRVL:	0

.NREL

-1
.INSC:	INSEC		;//EXTERNAL
.GTHT:	GETHT
.FRHT:	FRHT
.WNHT:	WNHT
.RELHT:	RELHT

RETRN:	0		;//RETURN ADDRESS

CKPARC:
	STA 3,RETRN	;//SAVE RETURN

	JSRII .INSC	;//CHECK INRSCTN OF WINDOW AND DISPLAY
	DSPX		;//INTERSECT TAKES PTRS TO THREE TABLES
.WNTB:	WINX		;//2 TO CMPAR & 1 RECEIVES INSCTN PARAMS
.REAL:	REALX		;//SKPRTRN IS NMAL--NOSKP MEANS NO INSCTN
	JMP OFDSP	;//IN THIS CASE WNDO AND DSP DON'T INSCT
	
	LDA 2,.WNTB	;//UPDATE WINDOW PARAMS
	LDA 3,.REAL	;//WITH INTERSECTION PARAMS
	LDA 0,0,3
	STA 0,0,2
	LDA 0,1,3
	STA 0,1,2
	LDA 0,2,3
	STA 0,2,2
	LDA 1,3,3
	STA 1,3,2
	JSRII .GTHT	;//SEE IF ROOM FOR LINE IN WINDOW
	SGE 1,0
	JMP NOROM	;//NO SCAN CONVERSION IF NOT
 
	LDA 1,@.FRHT	;//IF ROOM IN WINDOW FRAME HEIGHT
	SGE 1,0		;//SHOULD BE NO LESS THAN FONT HEIGHT
	STA 0,@.FRHT
 
	JSRII .INSC	;//NOW CHK FOR INSCTN OF WINDOW AND FRAME
	WINX
	FRMX
	REALX		;//RL TBL ARE PARAMS FOR ACTL SCAN CNVRSN
	JMP OFWIN
	LDA 0,@.WNHT	;//WINDOW HEIGHT SHOULD ALWAYS WIN
	STA 0,@.RELHT
	JMP @RETRN

OFDSP:
	ONE 0,0
REPLY:
	JSR @.SINS
	RP
	JMP @.EVAL
OFWIN:
	ONE 0,0
	INC 0,0
	JMP REPLY
NOROM:
	ONE 0,0
	INC 0,0
	INC 0,0
	JMP REPLY

.END
C